#
# Extended and customized version of prague's theme for prezto
#
# Authors:
#   Marek Fajkus <marek.faj@gmail.com>
#

function prompt_prague_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    branch_format="(%F{green}%b%f%u%c%F{cyan}●%f)"
  else
    branch_format="(%F{green}%b%f%u%c)"
  fi

  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"

  vcs_info 'prompt'

  # Get Ruby info
  if (( $+functions[ruby-info] )); then
    ruby-info
  fi

  # Get Virtual env info
  if (( $+functions[python-info] )); then
    python-info
  fi

  # Get Node info
  if (( $+functions[node-info] )); then
    node-info
  fi
}

function prompt_prague_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_prague_precmd

  # Formats:
  #   %b - branchname
  #   %u - unstagedstr (see below)
  #   %c - stagedstr (see below)
  #   %a - action (e.g. rebase-i)
  #   %R - repository path
  #   %S - path in the repository
  local branch_format="(%F{yellow}%b%f%u%c)"
  local action_format="(%F{green}%a%f)"
  local unstaged_format="%F{red}●%f"
  local staged_format="%F{green}●%f"

  # Set vcs_info parameters.
  zstyle ':vcs_info:*' enable bzr git hg svn
  zstyle ':vcs_info:*:prompt:*' check-for-changes true
  zstyle ':vcs_info:*:prompt:*' unstagedstr "${unstaged_format}"
  zstyle ':vcs_info:*:prompt:*' stagedstr "${staged_format}"
  zstyle ':vcs_info:*:prompt:*' actionformats "${branch_format}${action_format}"
  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"
  zstyle ':vcs_info:*:prompt:*' nvcsformats   ""

  # Set ruby-info parameters.
  zstyle ':prezto:module:ruby:info:version' format '%F{red}rb:%v%f'

  # Set python-info parameters.
  zstyle ':prezto:module:python:info:virtualenv' format '%F{yellow}py:%v%f'

  # Set node-info parameters
  zstyle ':prezto:module:node:info:version' format '%F{green}node:%v%f'

  # Define prompts.
  PROMPT="
%F{green}%n%f%F{yellow}@%m%f %F{208}λ%f %F{cyan}%~%f "'${vcs_info_msg_0_}'" "'${node_info[version]}'" "'${ruby_info[version]}'" "'$python_info[virtualenv]'"
%B%F{red}❯%F{yellow}❯%F{green}❯%f%b "
  RPROMPT=''
}

prompt_prague_setup "$@"
